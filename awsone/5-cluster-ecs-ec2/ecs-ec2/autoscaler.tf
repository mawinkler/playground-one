################################################################################
# Supporting Resources
################################################################################
module "autoscaling" {
  source  = "terraform-aws-modules/autoscaling/aws"
  version = "~> 7.3"

  for_each = {
    # On-demand instances currently disabled
    # See ecs-ec2.tf and service.tf to reenable
    #
    # # On-demand instances
    # asg-ondemand = {
    #   instance_type              = "t3.medium"
    #   use_mixed_instances_policy = false
    #   mixed_instances_policy     = {}
    #   user_data                  = <<-EOT
    #     #!/bin/bash
    #     cat <<'EOF' >> /etc/ecs/ecs.config
    #     ECS_CLUSTER=${module.ecs.cluster_name}
    #     ECS_LOGLEVEL=debug
    #     ECS_CONTAINER_INSTANCE_TAGS=${jsonencode(local.tags)}
    #     ECS_ENABLE_TASK_IAM_ROLE=true
    #     EOF
    #   EOT
    # }

    # Spot instances
    asg-spot = {
      instance_type              = "t3.medium"
      use_mixed_instances_policy = true
      mixed_instances_policy = {
        instances_distribution = {
          on_demand_base_capacity                  = 0
          on_demand_percentage_above_base_capacity = 0
          spot_allocation_strategy                 = "price-capacity-optimized"
        }

        override = [
          # Only using t3.medium, below would be an example to use mixed instances
          # {
          #   instance_type     = "t3.large"
          #   weighted_capacity = "2"
          # },
          {
            instance_type     = "t3.medium"
            weighted_capacity = "1"
          },
        ]
      }
      user_data = <<-EOT
        #!/bin/bash
        cat <<'EOF' >> /etc/ecs/ecs.config
        ECS_CLUSTER=${module.ecs.cluster_name}
        ECS_LOGLEVEL=debug
        ECS_CONTAINER_INSTANCE_TAGS=${jsonencode(local.tags)}
        ECS_ENABLE_TASK_IAM_ROLE=true
        ECS_ENABLE_SPOT_INSTANCE_DRAINING=true
        EOF
      EOT
    }
  }

  name = "${module.ecs.cluster_name}-${each.key}"

  # image_id      = jsondecode(data.aws_ssm_parameter.ecs_optimized_ami.value)["image_id"]
  image_id      = data.aws_ami.ecs_ami.id
  instance_type = each.value.instance_type

  security_groups                 = [module.autoscaling_sg.security_group_id]
  user_data                       = base64encode(each.value.user_data)
  ignore_desired_capacity_changes = true

  create_iam_instance_profile = true
  iam_role_name               = module.ecs.cluster_name
  iam_role_description        = "ECS role for ${module.ecs.cluster_name}"
  iam_role_policies = {
    AmazonEC2ContainerServiceforEC2Role = "arn:aws:iam::aws:policy/service-role/AmazonEC2ContainerServiceforEC2Role"
    AmazonSSMManagedInstanceCore        = "arn:aws:iam::aws:policy/AmazonSSMManagedInstanceCore"
  }

  vpc_zone_identifier = var.private_subnets
  health_check_type   = "EC2"
  min_size            = 1
  max_size            = 2
  desired_capacity    = 1

  # https://github.com/hashicorp/terraform-provider-aws/issues/12582
  autoscaling_group_tags = {
    AmazonECSManaged = true
  }

  # Required for  managed_termination_protection = "ENABLED"
  protect_from_scale_in = true

  # Spot instances
  use_mixed_instances_policy = each.value.use_mixed_instances_policy
  mixed_instances_policy     = each.value.mixed_instances_policy

  # Key name
  key_name = var.key_name

  # Tags
  tags = {
    Name          = "${var.environment}-ecs-ec2-autoscaler"
    Environment   = "${var.environment}"
    Product       = "playground-one"
    Configuration = "ecs-ec2"
  }
}

module "autoscaling_sg" {
  source  = "terraform-aws-modules/security-group/aws"
  version = "~> 5.1.2"

  name        = module.ecs.cluster_name
  description = "Autoscaling group security group"
  vpc_id      = var.vpc_id

  computed_ingress_with_source_security_group_id = [
    {
      rule                     = "http-80-tcp"
      source_security_group_id = module.alb_sg.security_group_id
    }
  ]
  ingress_with_cidr_blocks = [
    {
      rule        = "ssh-tcp"
      cidr_blocks = "${var.public_subnet_cidr_blocks[0]},${var.public_subnet_cidr_blocks[1]},${var.public_subnet_cidr_blocks[2]}"
    }
  ]
  number_of_computed_ingress_with_source_security_group_id = 1

  egress_rules = ["all-all"]

  tags = {
    Name          = "${var.environment}-ecs-ec2-autoscaler-security-group"
    Environment   = "${var.environment}"
    Product       = "playground-one"
    Configuration = "ecs-ec2"
  }
}
